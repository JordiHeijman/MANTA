[[model]]
name: Decker2009
desc: """
    Myokit implementation of Decker 2009 (aka HRd2009)

    Based on the matlab code found at http://rudylab.wustl.edu
    This code is known to differ slightly from the version given in the
    accompanying paper in its formulation of the Ca_JSR concentration.

    Typical pacing: 0.5ms at -80

    References
    - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
    [1] Properties and Ionic Mechanisms of Action Potential Adaptation,
    Restitution and Accomodation in Canine Epicardium.
    Decker, Heijman, Silva, Hund, Rudy
    2009, Am J Physiol Heart Circ Physiol
    doi:10.1152/ajpheart.01216.2008
    This paper comes with an online data supplement

    Original copyright notice:
    - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
    Copyright (C) 2008 by Keith F. Decker, Jordi Heijman, Thomas J. Hund
        Jonathan R. Silva and Yoram Rudy
    Email: rudy@wustl.edu
    Web:   http://rudylab.wustl.edu

    Port of the matlab code of the model described in the article
    "Properties and Ionic Mechanisms of Action Potential Adaptation,
    Restitution and Accomodation in Canine Epicardium"

    Am. J. Physiol. / Heart Circ. Physiol.
    Available online (January 23, 2009).
    doi:10.1152/ajpheart.01216.2008
    """
author: Michael Clerx, modified by Henry Sutanto -- Maastricht University 
"""
###Changelog###
Up to 22/03/2019 : 
- State-dependent markov INa has been implemented 
- Markov INa has been validated (drug-free, Flecainide, Lidocaine, Vernakalant) --> Further improvement might still be needed.
"""
# Initial values
membrane.V      = -87.4724138286382
calcium.uCa_i   = 0.01340869148018
calcium.uCa_sr  = 0.02414815836489
calcium.uCa_cal = 0.02510283012807
calcium.uCa_jsr = 6.99469048060168
calcium.Ca_nsr  = 1.159939751141822
calcium.fTrap   = 0.001886134160513385
sodium.Nai      = 6.935574785129178
sodium.Na_sr    = 6.935607113935819
potassium.K_i   = 145.5682623234052
chloride.Cl_i   = 20.2684217856671
chloride.Cl_sr  = 20.26842177086857
irel.Irel       = 0.001073322830982272
ina.h           = 0.9913109066867154
ina.m           = 0.001022252813581797
ina.j           = 0.9970771239754529
inal.a          = 0.001022252813581797
inal.i          = 0.3593255076983574
ito.a           = 1.773318268499115e-5
ito.i_fast      = 0.9979722824640838
ito.i_slow      = 0.987443528
ikr.a           = 1.235996442041028e-8
iclca.i_fast    = 0.9960352898692144
ical.C          = 0.9999613782488198
ical.O          = 2.27419494597377e-5
ical.Cs         = 5.936477396599124e-11
ical.Os         = 1.350122566369778e-15
ical.CI         = 1.587938121237188e-5
ical.OI         = 3.611420243201921e-10
ical.CIs        = 0
ical.OIs        = 0
iks.C1          = 0.476653873938053
iks.C2          = 0.2551771850026437
iks.C3          = 0.05122852186842027
iks.C4          = 0.004570874421666423
iks.C5          = 0.0001529389211563687
iks.C6          = 0.1328281770413747
iks.C7          = 0.05333228495514583
iks.C8          = 0.007137874613383642
iks.C9          = 0.0003184390970637536
iks.C10         = 0.01388061042363991
iks.C11         = 0.003715500164470033
iks.C12         = 0.0002486371465457659
iks.C13         = 0.0006446803014431032
iks.C14         = 8.628258026770862e-5
iks.C15         = 1.122823524761588e-5
iks.O1          = 1.051906959485266e-5
iks.O2          = 2.372219883411999e-6
"""
I_Na_Markov.C3 = 0.99
I_Na_Markov.C2 = 0.01
I_Na_Markov.C1 = 0  
I_Na_Markov.O  = 0  
I_Na_Markov.IF = 0  
I_Na_Markov.IC3 = 0 
I_Na_Markov.IC2 = 0 
I_Na_Markov.IM1 = 0 
I_Na_Markov.IM2 = 0 

I_Na_Markov.IFB = 0  
I_Na_Markov.IC3B = 0 
I_Na_Markov.IC2B = 0 
I_Na_Markov.OB = 0
"""
I_Na_Markov.IC3 = 0
I_Na_Markov.IC2 = 0
I_Na_Markov.IF = 0
I_Na_Markov.C3 = 1.00
I_Na_Markov.C2 = 0
I_Na_Markov.C1 = 0
I_Na_Markov.O = 0
I_Na_Markov.IS = 0
I_Na_Markov.DpIC3 = 0
I_Na_Markov.DpIC2 = 0
I_Na_Markov.DpIF = 0
I_Na_Markov.DpC3 = 0
I_Na_Markov.DpC2 = 0
I_Na_Markov.DpC1 = 0
I_Na_Markov.DpO = 0
I_Na_Markov.DpIS = 0
I_Na_Markov.DpIT = 0
I_Na_Markov.DIC3 = 0
I_Na_Markov.DIC2 = 0
I_Na_Markov.DIF = 0
I_Na_Markov.DC3 = 0
I_Na_Markov.DC2 = 0
I_Na_Markov.DC1 = 0
I_Na_Markov.DO = 0
I_Na_Markov.DIS = 0
I_Na_Markov.DIT = 0

[parameters]
ICaL_Block = 0.0
IK1_Block = 0.0
IKr_Block = 0.0
IKs_Block = 0.0
IKur_Block = 0.0
INa_Block = 0.0
INaL_Block = 0.0
INaK_Block = 0.0
INCX_Block = 0.0
Ito_Block = 0.0

Ca_o = 1.8 [mmol/L] : Extracellular Ca2+ concentration
K_o = 5.4 [mmol/L] : Extracellular K+ concentration
Na_o = 140 [mmol/L] : Extracellular Na+ concentration

[output]
Vm = membrane.V
Cai = 1000*calciumb.Ca_i
#dVdt = dot(membrane.V)

#
# Simulation engine variables
#
[engine]
time = 0 bind time
pace = 0 bind pace

#
# Total membrane currents and potential
#
[membrane]
markov_Na = 1
C = 1 [uF/cm^2]
dot(V) = -1/C * (i_ion + i_stim + i_diff)
    in [mV]
    label membrane_potential
    desc: The membrane potential
stim_amplitude = -80 [uA/uF]
i_stim = engine.pace * stim_amplitude
    desc: The stimulus current
    in [uA/uF]
i_diff = 0 bind diffusion_current
    desc: The diffusion current from cell to cell
    in [uA/uF]
i_ion = sodium.INa_tot + potassium.IK_tot + calcium.ICa_tot + chloride.ICl_tot
    desc: The remaining transmembrane currents
    in [uA/uF]

#
# Physical constants
#
[phys]
F = 96487 [C/mol] : Faraday's constant
R = 8314 [mJ/mol/K] : Gas constant
T = 310 [K] : Temperature
FRT = F/R/T
RTF = R*T/F

#
# Common terms
#
[common]
vf = membrane.V * phys.FRT
vff = vf * phys.F
evf = exp(vf)

#
# Cell geometry
#
[cell]
length = 0.01 [cm] : Cell length
radius = 0.0011 [cm] : Cell radius
volume = 3.14 * 1000 * radius * radius * length
    desc: Cell volume
    in [uL]
geoArea = 3.14 * 2 * radius * radius + 2 * 3.141592653589793 * radius * length
    desc: Geometric membrane area
    in [cm^2]
    # Two different versions of PI are used to obtain the same results as in
    #  the online matlab reference implementation.
A = 2 * geoArea
    desc: Capacitive membrane area
AF = A / phys.F
    desc: The capacitive membrane area divided by Faraday's constant (section Q)
v_cyt = volume * 0.678 [uL]
    desc: Myoplasm volume (section Q)
v_nsr = volume * 0.0552 [uL]
    desc: Volume of the network sarcoplasmic reticulum
v_jsr = volume * 0.0048 [uL]
    desc: Volume of the junctional sarcoplasmic reticulum
v_sr = volume * 0.02 [uL]
    desc: """
    SR Subspace (SS,SR) volume

    This space between the INaCa,ss channel, the IClCa channel and the
    Junctional Sarcoplasmic Reticulum (JSR) has no strict physical
    boundaries but is included in the model because it is a region of the
    cell that has slightly different concentrations of Ca, Na and K.
    """
v_cal = volume * 0.002 [uL]
    desc: """
    L-type Ca2+ current Subspace (SS,CaL) volume

    The area between the SR subspace and the ICaL channel has a slightly
    different Ca2+ concentration than the rest of the cell.
    """

#
# Extracellular concentrations
#
[extra]
Ko  = parameters.K_o : Extracellular potassium concentration   # Same as LR1
Cao = parameters.Ca_o : Extracellular calcium concentration     # Same as LR1
Nao = parameters.Na_o : Extracellular sodium concentration      # Same as LR1
Clo = 100 [mM] : Extracellular chloride concentration

#
# Reversal potentials
#
[nernst]
EK = phys.RTF * log(extra.Ko / potassium.K_i)
    desc: Nernst potential for Potassium currents
    in [mV]
P_NaK = 0.01833 : IKs sodium / potassium permeability ratio
EKs  = phys.RTF * log((extra.Ko + P_NaK * extra.Nao) / (potassium.K_i + P_NaK * sodium.Nai))
    desc: Modified Nernst potential for IKs
    in [mV]
ENa = phys.RTF * log(extra.Nao / sodium.Nai)
    desc: Reversal potential for Sodium currents
    in [mV]
ECl = -phys.RTF * log(extra.Clo / chloride.Cl_i)
    desc: Reversal potential for Chloride currents
    in [mV]

#
# Calcium in subspaces
# Described in sections P and Q
#
[calcium]
use membrane.V as V
use calciumb.Ca_i
# Iup
Km_CaMK = 0.15
DKm_PLB = 0.00017
DIupBar = 0.75
Km_up   = 0.00092
IupBar  = 0.004375
Iup = (dJup + 1) * IupBar * Ca_i / (Ca_i + Km_up - dKmPLB)
    zz     = calcium.active / (Km_CaMK + calcium.active)
    dKmPLB = DKm_PLB * zz
    dJup   = DIupBar * zz
    desc: Ca2+ uptake from myoplasm to NSR
    in [mM/ms]
# Ileak
NSRBar = 15
Ileak = 0.004375 * Ca_nsr / NSRBar
    desc: Ca2+ leask from JSR to myoplasm
    in [mM/ms]
# Itr
Itr = (Ca_nsr - Ca_jsr) / 25
    desc: Ca2+ transfer from NSR to JSR (section P)
    in [mM/ms]
# Unbuffered Calcium SR and cal
dot(uCa_sr) = (
        inaca.INaCaSS * cell.AF / cell.v_sr
        + irel.Irel * cell.v_jsr / cell.v_sr
        - (diff.Ca + diff.CaL)
        )
dot(uCa_cal) = (
        - ical.ICaL * cell.AF / (cell.v_cal * 2)
        + diff.CaL * cell.v_sr / cell.v_cal
        )
# Ca_JSR
Km_csqn = 0.8
csqnBar = 10
dot(uCa_jsr) =  Itr - irel.Irel
Ca_jsr = (sqrt(b*b + 4*c) - b) / 2
    b = csqnBar + Km_csqn - uCa_jsr     # Differs from paper!
    c = uCa_jsr * Km_csqn               # Differs from paper!
# Ca_NSR (No buffer formulation is used for Ca_NSR)
dot(Ca_nsr) =  Iup - Ileak - Itr * cell.v_jsr / cell.v_nsr
# Active CaMKII
alpha = 0.05
beta  = 6.8e-4
CaMK0  = 0.05
Km_CaM = .0015
active = CaMK0 * (1 - fTrap) / (1 + Km_CaM / calciumb.Ca_sr) + fTrap
dot(fTrap) =  alpha * active * (active - fTrap) - beta * fTrap
    desc: Fraction of trapped calmodulin
ICa_tot = (ical.ICaL
        + icab.ICab
        + ipca.IpCa
        - inaca.INaCa * 2
        - inaca.INaCaSS * 2 )
    desc: Total Calcium current through the membrane
    in [uA/uF]
# Internal Calcium
dot(uCa_i) = (diff.Ca * cell.v_sr / cell.v_cyt
         - (icab.ICab + ipca.IpCa - 2 * inaca.INaCa) * cell.AF / (cell.v_cyt * 2)
         - (calcium.Iup - calcium.Ileak) * cell.v_nsr / cell.v_cyt)

#
# Buffered Calcium concentrations
#
[calciumb]
use calcium.uCa_i
use calcium.uCa_sr
use calcium.uCa_cal
# Ca_SR, Ca_CaL
BSRBar = 0.047
BSLBar = 1.124
BS_sum = BSRBar + BSLBar
Km_BSR = 0.00087
Km_BSL = 0.0087
Km_sum2 = Km_BSR + Km_BSL
Km_pro2 = Km_BSR * Km_BSL
sum2 = BS_sum + Km_sum2
pro_sum2 = Km_pro2 + BSRBar * Km_BSL + BSLBar * Km_BSR
Ca_sr = (- b / 3
        + (2 / 3) * sqrt(b*b - 3 * c)
        * cos(acos((9*b*c - 2*b^3 - 27*d) / (2 * (b*b - 3 * c)^1.5))/3)
        )
    b = sum2 - uCa_sr
    d = -Km_pro2 * uCa_sr
    c = pro_sum2 - Km_sum2 * uCa_sr
    in [mM]
Ca_cal = (- b / 3
        + (2 / 3) * sqrt(b*b - 3 * c)
        * cos(acos((9*b*c - 2*b^3 - 27*d) / (2 * (b*b - 3 * c)^1.5))/3)
        )
    b = sum2 - uCa_cal
    d = -Km_pro2 * uCa_cal
    c = pro_sum2 - Km_sum2 * uCa_cal
    in [mM]
Ca_i = - b / 3 + (2/3) * sqrt(b*b - 3*c) * cos(
       acos((9*b*c - 2*b^3 - 27*d) / (2 * (b*b - 3*c)^1.5)) / 3)
    b = sum - uCa_i
    c = pro - uCa_i * Km_sum
    d = -Km_pro * uCa_i
    cmdnBar = 5.e-02
    trpnBar = 7.e-02
    Km_cmdn = 2.38e-03
    Km_trpn = 5.e-04
    Km_sum = Km_trpn + Km_cmdn
    Km_pro = Km_cmdn * Km_trpn
    sum = cmdnBar + trpnBar + Km_trpn + Km_cmdn
    pro = Km_pro + trpnBar * Km_cmdn + cmdnBar * Km_trpn
    in [mM]

#
# Chloride concentrations
# Described in section Q
#
[chloride]
dot(Cl_i) =  r1 * iclb.IClB + r2 * diff.Cl + cot.CTNaCl + cot.CTKCl
    r1 = cell.AF / cell.v_cyt
    r2 = cell.v_sr / cell.v_cyt
    desc: Intracellular Chloride concentration
    in [mM]
dot(Cl_sr) =  iclca.IClCa * r1 - diff.Cl
    r1 = cell.AF / cell.v_sr
    desc: Intracellular Chloride concentration in the SR subspace (SS,SR)
    in [mM]
ICl_tot = iclb.IClB + iclca.IClCa
    desc: Total chloride current through the membrane
    in [uA/uF]

#
# Sodium concentrations
# Described in section Q
#
[sodium]
dot(Nai) =  -r1 * INa_cyt + r2 * diff.Na + cot.CTNaCl
    r1 = cell.AF / cell.v_cyt
    r2 = cell.v_sr / cell.v_cyt
    desc: Intracellular sodium concentration
    in [mM]
dot(Na_sr) =  -(3 * inaca.INaCaSS * r1 + diff.Na)
    r1 = cell.AF / cell.v_sr
    desc: Intracellular sodium concentration in the SR subspace (SS,SR)
    in [mM]
INa_cyt = ((if(membrane.markov_Na == 1, I_Na_Markov.I_Na_tot, ina.INa))
        + inal.INaL
        + inab.INaB
        + inak.INaK * 3
        + inaca.INaCa * 3 )
    in [uA/uF]
INa_tot = INa_cyt + inaca.INaCaSS * 3
    desc: Total Sodium current
    in [uA/uF]

#
# Potassium concentrations
# Described in section Q
#
[potassium]
dot(K_i) =  -potassium.IK_tot * (cell.AF / cell.v_cyt) + cot.CTKCl
    desc: Potassium concentrations
    in [mM]
IK_tot = (ik1.IK1
       + ikr.IKr
       + iks.IKs
       + ikur.IKur
       + ito.ITo
       - inak.INaK * 2)
   in [uA/uF]
# Note: removed stimulus current from this sum

#
# ICaL
# Described in section A of the supplement to [1]
#
[ical]
use membrane.V
use common.vf, common.vff
p00 = 0.59
p01 = 0.8
p02 = 0.052
p03 = 13
p04 = 0.132
p05 = 13.56
p06 = 9.45
p07 = 24.828
p08 = 49.1
p09 = 10.349
p10 = 30.553
p11 = 0.213
p12 = 10.807
p13 = 17.5    # Default: 17.5, lower than 13.5 causes very long APDs
p14 = 3       # Default value: 3, 8 gives EADs
p15 = 0.25
p16 = 25
p17 = 17.5
p18 = 0.0001  # Default: 0.0001
p19 = 3
p20 = 1e-06
p21 = 0.00015552
    desc: Scaling factor for conductance
# xx and yy
I_V1   = p07 * (1 + exp((V + p08) / p09))
I_V2   = p10 * (1 + exp(-(V + p11) / p12))
I_Vss  = (p15 + 1 / (1 + exp((V + p13) / p14))) / (1 + p15)
I_Vtau = 1 / (1 / I_V1 + 1 / I_V2)
xx = I_Vss / I_Vtau
yy = (1 - I_Vss) / I_Vtau
# xs and ys
zz      = (0.003 / calciumb.Ca_cal)^4
Is_Vss  = (p18 + 1 / (1 + exp((V + p13) / p14)) ) / (1 + p18)
Is_Ca   = p16 - (p17 / (1 + zz))
Is_V2   = Is_Ca * (1 + exp(-(V + p11) / p12))
Is_Vtau = 1 / (1 / I_V1 + 1 / Is_V2)
xs = Is_Vss / Is_Vtau
ys = (1 - Is_Vss) / Is_Vtau
# alpha, beta, delta, theta
ACT_tau = p00 + p01 * exp(p02 * (V + p03)) / (1 + exp(p04 * (V + p03)))
ACT_ss  = 1 / (1 + exp((p05 - V + 4.3) / p06))
alpha   = ACT_ss / ACT_tau
beta    = (1 - ACT_ss) / ACT_tau
delta   = p19 / (1 + zz)
theta   = 1
theta_I = p20
delta_I = if(numer == 0, 0, numer / denom)
    numer = (xx * ys * delta) * theta_I
    denom = (yy * xs * theta)
# States
dot(C)   = -(alpha + delta   + yy) * C   + beta  * O  + theta   * Cs  + xx    * CI
dot(O)   = -(beta  + delta   + yy) * O   + alpha * C  + theta   * Os  + xx    * OI
dot(Cs)  = -(alpha + theta   + ys) * Cs  + delta * C  + beta    * Os  + xs    * CIs
dot(Os)  = -(beta  + theta   + ys) * Os  + delta * O  + alpha   * Cs  + xs    * OIs
dot(CI)  = -(alpha + delta_I + xx) * CI  + yy    * C  + theta_I * CIs + beta  * OI
dot(OI)  = -(beta  + delta_I + xx) * OI  + yy    * O  + theta_I * OIs + alpha * CI
dot(CIs) = -(alpha + theta_I + xs) * CIs + ys    * Cs + delta_I * CI  + beta  * OIs
dot(OIs) = -(beta  + theta_I + xs) * OIs + ys    * Os + delta_I * OI  + alpha * CIs
ICaL = (1.0 - parameters.ICaL_Block) * ICaLBar * g
    g = O + Os
    gCai = 1
    gCao = 0.341
    ICaLBar = if(e2v == 1,
            p21 * 4 * phys.F * phys.FRT * (gCai * calciumb.Ca_cal - gCao * extra.Cao) / (2 * phys.FRT),
            p21 * 4 * vff * (gCai * calciumb.Ca_cal * e2v - gCao * extra.Cao) / (e2v - 1))
        e2v = exp(2 * vf)
    in [uA/uF]

#
# Markov model of IKs
# Described in section B
#
[iks]
use membrane.V
use calciumb.Ca_i
use common.vf
p00 =  0.01486459798086
p01 =  0.02987730123588
p02 =  0.08398631219983
p03 = -0.05546105712664
p04 =  0.01460066118316
p05 =  0.24464953099645
p06 =  0.00311732688740
p07 = -0.42625451944376
p08 =  0.07731990097331
p09 = -0.06472612248871
p10 =  0.08953830641102
p11 =  0.79405459958640
p12 = -0.08017378192977
p13 =  0.58638228663014
p14 =  0.28205554331496
p15 =  0.0826
    desc: Maximal conductance scaling
a = p00 * exp(vf * p01)
b = p02 * exp(vf * p03)
g = p04 * exp(vf * p05)
d = p06 * exp(vf * p07)
e = p08 * exp(vf * p09)
t = p10
o = p11 * exp(vf * p12)
p = p13 * exp(vf * p14)
# States
dot(C1)  =            b*C2                    -C1  * (4*a)
dot(C2)  = 4*a*C1  +2*b*C3           +  d*C6  -C2  * (3*a +  b +  g)
dot(C3)  = 3*a*C2  +3*b*C4           +  d*C7  -C3  * (2*a +2*b +2*g)
dot(C4)  = 2*a*C3  +4*b*C5           +  d*C8  -C4  * (  a +3*b +3*g)
dot(C5)  =   a*C4                    +  d*C9  -C5  * (     4*b +4*g)
dot(C6)  =            b*C7  +  g*C2           -C6  * (3*a           +  d)
dot(C7)  = 3*a*C6  +2*b*C8  +2*g*C3  +2*d*C10 -C7  * (2*a +  b +  g +  d)
dot(C8)  = 2*a*C7  +3*b*C9  +3*g*C4  +2*d*C11 -C8  * (  a +2*b +2*g +  d)
dot(C9)  =   a*C8           +4*g*C5  +2*d*C12 -C9  * (     3*b +3*g +  d)
dot(C10) =            b*C11 +  g*C7           -C10 * (2*a           +2*d)
dot(C11) = 2*a*C10 +2*b*C12 +2*g*C8  +3*d*C13 -C11 * (  a +  b +  g +2*d)
dot(C12) =   a*C11          +3*g*C9  +3*d*C14 -C12 * (     2*b +2*g +2*d)
dot(C13) =            b*C14 +  g*C11          -C13 * (  a           +3*d)
dot(C14) =   a*C13          +2*g*C12 +4*d*C15 -C14 * (       b +  g +3*d)
dot(C15) =                     g*C14          -C15 * (               4*d + t) + e*O1
dot(O1)  = -(e + p) * O1 + o * O2 + t * C15
dot(O2)  =       p  * O1 - o * O2
IKs = (1.0 - parameters.IKs_Block) * GKs * (O1 + O2) * (V - nernst.EKs)
    in [uA/uF]
GKs  = p15 * (1 + 0.6 / (1 + (3.8e-5 / Ca_i) ^ 1.4))
    desc: IKs maximal conductance

#
# INa
# Described in section G of the supplement to [1]
#
# Identical to the original Luo-Rudy (Guinea pig) formulation.
#
[ina]
use membrane.V as V
Gbar = 9.075
INa = (1.0 - parameters.INa_Block) * Gbar * m ^ 3 * h * j * (V - nernst.ENa)
    in [uA/uF]
dot(m) =  alpha * (1 - m)  - beta * m
    alpha = if(dd == 0, 3.2, 0.32 * vv / dd)
        vv = V + 47.13
        dd = 1 - exp(-0.1 * vv)
    beta  = 0.08 * exp(-V / 11)
dot(h) =  alpha * (1 - h) - beta * h
    alpha = if(V >= -40, 0, 0.135 * exp((80 + V) / -6.8))
    beta  = if(V >= -40,
            1 / (0.13 * (1 + exp((V + 10.66) / -11.1))),
            3.56 * exp(0.079 * V) + 3.1e5 * exp(0.35 * V))
dot(j) =  alpha * (1 - j) - beta * j
    alpha = if(V >= -40, 0,
              (-1.2714e5 * exp(0.2444 * V) - 6.948e-5 * exp(-0.04391 * V))
            * (V + 37.78) / (1 + exp(0.311 * (V + 79.23))))
    beta  = if(V >= -40,
            0.3 * exp(-2.535e-7 * V) / (1 + exp(-0.1 * (V + 32))),
            0.1212 * exp(-0.01052 * V) / (1 + exp(-0.1378 * (V + 40.14))))

########################################################
[I_Na_Markov]
# this state dependence of Na channel was from Moreno et al 2011 (Sci. Transl. Med)
# contains free, charged, neutral drug

class1_drug_cat = 3
Lidocaine_fact = if(class1_drug_cat==2 and new_drug_mode==0, 1, 0) ## example for Ib
Flecainide_fact = if(class1_drug_cat==3 and new_drug_mode==0, 1, 0) ## example for Ic
Vernakalant_fact = if(class1_drug_cat==4 and new_drug_mode==0, 1, 0) 

x1 = 0.33895924364090035
x2 = 0.69136195942957679
x3 = 1.4310575051533165
x4 = 0.65294099974057818
x5 = 2.240616847042463
x6 = 2.7473947858770513
x7 = 3.8717514280429359
x8 = 0.77360525533046953
x9 = 0.48988864341477356
x10 = 0.16587684783843748
x11 = 0.79166905392591369
x12 = 0.97426986866050913
x13 = 1.5371091566498705
x14 = 1.0328344
x15 = 0.99481919
x16 = 1.00254845
x17 = 1.01096423
x18 = 1.27052465
x19 = 1.24650782516868182
x20 = 0.40525158265996508
x21 = 0.9911675441375104
x22 = 1.1141648721471809
x23 = 0.7400221830311837
x24 = 0.6867087130674105
x25 = 0.9613389122782059

f1 = 0.99894802383223924 #15802.517082265076
f2 = 1.011763238332033 #11212.49445307003
f3 = 1.0601752630895926 #1.0835911457972944
f4 = 1.0304361379208964 #4.7213138217409707
f5 = 1.0488020784823679 #1.1886854610200599
f6 = 1.006194635479259 #1.1293915676022106
f7 = 1.0196751172280454 #0.98647121091698153
f8 = 11.380184354561393 #0.54542349779755606
f9 = 1.0258563336302067 #1.020713680006784
f10 = 0.75435783615544616 #2.6609951303941308
f11 = 1.0108975941170679 #1.1411905523331658
f12 = 0.97128471748753387 #5.3540204782968566
f13 = 1.0075653020362194 #1.028853996176295
f14 = 1.0148100245843019 #1.0033797797467918

l1 = 1.36922845112808845e+04
l2 = 1.36922845112808845e+04
l3 = 9.75801811222358206e-01
l4 = 4.78855031291120348e+00
l5 = 9.75801811222358206e-01
l6 = 9.78288593348167645e-01
l7 = 9.78288593348167645e-01
l8 = 9.05788719283946997e-01
l9 = 9.05788719283946997e-01
l10 = 4.84105307990853273e+00

v1 = 1643.4118672121467 #7170.4622644564224
v2 = 500#1203.7661761097061 #15352.068978379524
v3 = 0.29032680374507414 #0.78329295519833497
v4 = 1.145809054293967 #6.2271179964787571
v5 = 19.394482038525979 #22.10184390723893
v6 = 0.44798732782313322 #1.5107146709941404
v7 = 0.29031744983038188 #1.0935642293370962
v8 = 1.2169186721862526 #0.65910138578597266
v9 = 1.3533338540760118 #2.3700350948377
v10 = 0.92848089050303395 #6.2071593539114822
v11 = 0
v12 = 0
v13 = 0
v14 = 0

#### new drug components ####
new_drug_mode = 0
n_diffusion = 0
n_kd_open = 0
n_k_off = 0
n_ki_off = 0
n_kc_off = 0
n_p22 = 0
n_p23 = 0
n_p24 = 0
n_p25 = 0
n_p26 = 0
n_p27 = 0
n_p28 = 0
n_p29 = 0
n_p30 = 0
n_p31 = 0
n_p32 = 0
n_p33 = 0
n_p34 = 0
n_p35 = 0

diffusion = ((new_drug_mode*n_diffusion) + (Flecainide_fact*5500) + (Lidocaine_fact*500) + (Vernakalant_fact*500*6.4629447592936096)) #in [1 / M ms]
kd_open = ((new_drug_mode*n_kd_open) + (Flecainide_fact*11.2e-6) + (Lidocaine_fact*318e-6) + (Vernakalant_fact*318e-6*0.34682644956847741)) * exp(-0.7 * membrane.V * phys.FRT)
conc = 0
pH = 7.4
pKa_newdrug = 7.4
pKa_Lidocaine = 7.6
pKa_Flecainide = 9.3
pKa_Vernakalant = 5.4
#pH = (if(Lidocaine_fact==1, pH_Lidocaine, 0)) + (if(Flecainide_fact==1, pH_Flecainide, 0)) + (if(new_drug_mode==1, pH_newdrug, 0))
pKa = (if(Vernakalant_fact==1, pKa_Vernakalant, 0)) + (if(Lidocaine_fact==1, pKa_Lidocaine, 0)) + (if(Flecainide_fact==1, pKa_Flecainide, 0)) + (if(new_drug_mode==1, pKa_newdrug, 0))
portion = 1/(1+10^(pH-pKa))
conc_dplus = portion * conc
conc_d = (1-portion) * conc

kon = conc_dplus * diffusion #if(conc_dplus>0.0001, conc_dplus * diffusion, 1e-8) #### for protonated drugs
kcon = kon
koff = kd_open * diffusion
kcoff = koff

k_on = conc_d * diffusion #if(conc_d>0.0001, , 1e-8) #### for neutral drugs
k_off = ((new_drug_mode*n_k_off) + (Flecainide_fact*400e-6) + (Lidocaine_fact*400e-6) + (Vernakalant_fact*400e-6*0.51684933468012062)) * diffusion
ki_on = k_on / 2
ki_off = ((new_drug_mode*n_ki_off) + (Flecainide_fact*5.4e-6) + (Lidocaine_fact*3.4e-6) + (Vernakalant_fact*3.4e-6*0.62400593362193568)) * diffusion
kc_on = k_on / 2
kc_off = ((new_drug_mode*n_kc_off) + (Flecainide_fact*800e-6) + (Lidocaine_fact*900e-6) + (Vernakalant_fact*900e-6*0.045971749343871632)) * diffusion

Tfactor = 1 / (3 ^ ((37 - (phys.T - 273)) / 10.0)) #0.19245

# Transition rates (ms-1)  
a11 = Tfactor * p1 / (p2 * exp(-(membrane.V - actshift) / p3) + p6 * exp(-(membrane.V - actshift) / p7))
a12 = Tfactor * p1 / (p2 * exp(-(membrane.V - actshift) / p4) + p6 * exp(-(membrane.V - actshift) / p7))
a13 = Tfactor * p1 / (p2 * exp(-(membrane.V - actshift) / p5) + p6 * exp(-(membrane.V - actshift) / p7))
b11 = Tfactor * p8 * exp(-(membrane.V - actshift) / p9)
b12 = Tfactor * p10 * exp(-(membrane.V - actshift - p11) / p9)
b13 = Tfactor * p12 * exp(-(membrane.V - actshift - p13) / p9)
#a3 = p14 * exp(-membrane.V / p15)
#b3 = p16 * exp(membrane.V / p17)
#aa = a3 / (a3 + b3)
#tau = 1 / (a3 + b3)
a3_ss = 1/(1+exp((membrane.V - p14_new) / p15_new))
a3_tau = h1 + p01 * exp(p16_new * (membrane.V - p14_new)) / (1 + exp(p17_new * (membrane.V - p14_new)))
a3 = Tfactor * a3_ss / a3_tau
b3 = Tfactor * (1 - a3_ss) / a3_tau
a2 = Tfactor * p18 * exp(membrane.V / p19)
b2 = (a13*a2*a3) / (b13*b3)
ax = p20 * a2
bx = p21 * a3

a13c = p22 * a13
b13c = if(kon>0, (b13 * kcon * koff * a13c) / (kon * kcoff * a13), 0)
a13n = p23 * a13
b13n = if(k_on>0, (b13 * kc_on * a13n * k_off) / (kc_off * a13 * k_on), 0)
ax1 = p24 * ax
bx1 = p25 * bx
ax2 = p26 * ax
bx2 = if(ki_on>0, (bx * k_on * ax2 * ki_off) / (ax * ki_on * k_off), 0)
a22 = p27 * a2
a_22 = p28 * a2
b22 = if(b13c>0, (a13c * a22 * a33) / (b13c * b33), 0)
b_22 = if(b13n>0, (a_33 * a13n * a_22) / (b_33 * b13n), 0)
b33 = p29 * b3
b_33 = p30 * b3
a33 = p31 * a3
a_33 = if(ki_on>0, (ki_off * a3 * kc_on * b_33) / (ki_on * kc_off * b3), 0)
## DIT or DpIT ONLY in Lidocaine (Inactivated, Trapped state)
a44 = p32 * a2
b44 = p33 * a3
a_44 = p34 * a2
b_44 = p35 * a2

actshift = -15*x19
h1 = 2*x20
p1 = 8.5539*x1
p2 = 7.4392e-2*x2
p3 = 17.0*x3
p4 = 15.0*x4
p5 = 12.0*x5
p6 = 2.0373e-1*x6
p7 = 150*x7
p8 = 7.5215e-2*x8
p9 = 20.3*x9
p10 = 2.7574*x10
p11 = 5*x11
p12 = 4.7755e-1*x12
p13 = 10*x13
p14 = 5.1458e-6 #5.1458e-6 *1.0325463712122342
p15 = 8.2471 #0.8*8.2471 * 0.10401603829601243
p16 = 6.1205 #6.1205 * 0.96668483687506801
p17 = 12.542 #0.8*12.542 * 1.0908872227689068
p18 = 13.370*x14
p19 = 43.749*x15
p20 = 3.4229e-2*x16
p21 = 1.7898e-2*x17
p14_new = -70*x21
p15_new = 3.5*x22
p16_new = 0.052 * 2.9*x23
p17_new = 0.132 * 1.9*x24
p01 = 41*x25

p22 = (new_drug_mode*n_p22) + (Flecainide_fact*3.6324e-3*f1) + (Lidocaine_fact*5.6974e-03*l1) + (Vernakalant_fact*5.6974e-03*v1) #3.6324e-3
p23 = (new_drug_mode*n_p23) + (Flecainide_fact*2.6452*f2) + (Lidocaine_fact*8.4559e+01*l2) + (Vernakalant_fact*8.4559e+01*v2) #2.6452
p24 = (new_drug_mode*n_p24) + (Flecainide_fact*5.7831e-5*f3) + (Lidocaine_fact*6.3992e-07*l3) + (Vernakalant_fact*6.3992e-07*v3) #5.7831e-5
p25 = (new_drug_mode*n_p25) + (Flecainide_fact*1.6689e-8*f4) + (Lidocaine_fact*1.3511e+00*l4) + (Vernakalant_fact*1.3511e+00*v4) #1.6689e-8
p26 = (new_drug_mode*n_p26) + (Flecainide_fact*2.6126e-01*f5) + (Lidocaine_fact*1.3110e-01*l5) + (Vernakalant_fact*1.3110e-01*v5) #2.6126e-01
p27 = (new_drug_mode*n_p27) + (Flecainide_fact*1.4847e3*f6) + (Lidocaine_fact*6.7067e-06*l6) + (Vernakalant_fact*6.7067e-06*v6) #1.4847e3
p28 = (new_drug_mode*n_p28) + (Flecainide_fact*4.2385e+01*f7) + (Lidocaine_fact*1.7084e-05*l7) + (Vernakalant_fact*1.7084e-05*v7) #4.2385e+01
p29 = (new_drug_mode*n_p29) + (Flecainide_fact*1.7352e-6*f8) + (Lidocaine_fact*1.9698e-05*l8) + (Vernakalant_fact*1.9698e-05*v8) #1.7352e-6
p30 = (new_drug_mode*n_p30) + (Flecainide_fact*2.1181e+00*f9) + (Lidocaine_fact*4.8477*l9) + (Vernakalant_fact*4.8477*v9) #2.1181e+00
p31 = (new_drug_mode*n_p31) + (Flecainide_fact*6.7505e-05*f10) + (Lidocaine_fact*3.2976*l10) + (Vernakalant_fact*3.2976*v10) #6.7505e-05
p32 = (new_drug_mode*n_p32) + (Flecainide_fact*2.4135*f11) + (Lidocaine_fact*0) + (Vernakalant_fact*2.4135*v11)
p33 = (new_drug_mode*n_p33) + (Flecainide_fact*4.9001e-2*f12) + (Lidocaine_fact*0) + (Vernakalant_fact*4.9001e-2*v12) 
p34 = (new_drug_mode*n_p34) + (Flecainide_fact*1.0326e-03*f13) + (Lidocaine_fact*0) + (Vernakalant_fact*1.0326e-03*v13) 
p35 = (new_drug_mode*n_p35) + (Flecainide_fact*2.1378e-02*f14) + (Lidocaine_fact*0) + (Vernakalant_fact*2.1378e-02*v14)

dot(IC3) = -IC3 * (a11 + a3 + ki_on) + IC2 * b11 + C3 * b3 + ki_off * DIC3
dot(IC2) = -IC2 * (b11 + a3 + a12 + ki_on) + IC3 * a11 + IF * b12 + C2 * b3 + ki_off * DIC2
dot(IF) = -IF * (b12 + a3 + b2 + ki_on) + IC2 * a12 + C1 * b3 + O * a2 + ki_off * DIF
dot(C3) = -C3 * (b3 + a11 + kcon + kc_on) + IC3 * a3 + C2 * b11 + DpC3 * kcoff + DC3 * kc_off
dot(C2) = -C2 * (b11 + b3 + a12 + kcon + kc_on) + C3 * a11 + IC2 * a3 + C1 * b12 + DpC2 * kcoff + DC2 * kc_off
dot(C1) = -C1 * (b12 + b3 + a13 + kcon + kc_on) + C2 * a12 + IF * a3 + O * b13 + DpC1 * kcoff + DC1 * kc_off
dot(O) = -O * (b13 + a2 + ax + kon + k_on) + C1 * a13 + IF * b2 + IS * bx + DpO * koff + DO * k_off
dot(IS) = -IS * (bx + ki_on) + O * ax + DIS * ki_off

dot(DpIC3) = -DpIC3 * (a33 + a11) + DpIC2 * b11 + DpC3 * b33
dot(DpIC2) = -DpIC2 * (b11 + a33 + a12) + DpIC3 * a11 + DpIF * b12 + DpC2 * b33
dot(DpIF) = -DpIF * (b12 + a33 + b22 + a44) + DpIC2 * a12 + DpC1 * b33 + DpO * a22 + DpIT * b44
dot(DpC3) = -DpC3 * (b33 + a11 + kcoff) + DpIC3 * a33 + DpC2 * b11 + C3 * kcon
dot(DpC2) = -DpC2 * (b11 + b33 + a12 + kcoff) + DpC3 * a11 + DpIC2 * a33 + DpC1 * b12 + C2 * kcon
dot(DpC1) = -DpC1 * (b12 + b33 + a13c + kcoff) + DpC2 * a12 + DpIF * a33 + DpO * b13c + C1 * kcon
dot(DpO) = -DpO * (b13c + a22 + ax1 + koff) + DpC1 * a13c + DpIF * b22 + DpIS * bx1 + O * kon
dot(DpIS) = -DpIS * (bx1) + DpO * ax1
dot(DpIT) = -DpIT * b44 + DpIF * a44

dot(DIC3) = -DIC3 * (a_33 + a11 + ki_off) + DIC2 * b11 + DC3 * b_33 + ki_on * IC3
dot(DIC2) = -DIC2 * (b11 + a_33 + a12 + ki_off) + DIC3 * a11 + DIF * b12 + DC2 * b_33 + ki_on * IC2
dot(DIF) = -DIF * (b12 + a_33 + b_22 + a_44 + ki_off) + DIC2 * a12 + DC1 * b_33 + DO * a_22 + DIT * b_44 + ki_on * IF
dot(DC3) = -DC3 * (b_33 + a11 + kc_off) + DIC3 * a_33 + DC2 * b11 + C3 * kc_on
dot(DC2) = -DC2 * (b11 + b_33 + a12 + kc_off) + DC3 * a11 + DIC2 * a_33 + DC1 * b12 + C2 * kc_on
dot(DC1) = -DC1 * (b12 + b_33 + a13n + kc_off) + DC2 * a12 + DIF * a_33 + DO * b13n + C1 * kc_on
dot(DO) = -DO * (b13n + a_22 + ax2 + k_off) + DC1 * a13n + DIF * b_22 + DIS * bx2 + O * k_on
dot(DIS) = -DIS * (bx2 + ki_off) + DO * ax2 + IS * ki_on
dot(DIT) = -DIT * b_44 + DIF * a_44

#E_Na = (phys.RTF) * log(138/10)
g_Na = x18*7.8 *1.0 *4.5 #7.8 * 1.0 * 4.7#18 #13#4.5##*7.1#*91.5
    in [1/ms]
I_Na_tot = (1 - parameters.INa_Block) * g_Na * O * (membrane.V - nernst.ENa)
    in [pA]

########################################################
# Late sodium current
# Described in section O
#
[inal]
use membrane.V as V
Gbar = .0065
INaL = (1.0 - parameters.INaL_Block) * Gbar * a^3 * i * (V - nernst.ENa)
    desc: Late sodium current
    in [uA/uF]
dot(a) =  alpha * (1 - a) - beta * a
    alpha = if(dd == 0, 3.2, 0.32 * vv / dd)
        vv = V + 47.13
        dd = 1 - exp(-0.1 * vv)
    beta  = 0.08 * exp(-V / 11)
dot(i) =  (hLss - i) / 600
    hLss = 1 / (1 + exp((V + 91) / 6.1))

#
# Background sodium current
# Described in section H
#
[inab]
use common.vf, common.evf, common.vff
use sodium.Nai, extra.Nao
P = 3.2e-9
INaB = if(evf == 1,
        P * phys.F * (Nai - Nao),
        P * vff * (Nai * evf - Nao) / (evf - 1))
    in [uA/uF]

#
# Sodium potassium exchanger
# A recently developed I(NaK) formulation based on data from canine left
#  ventricular epicardial myocytes.
# Described in section C
#
[inak]
VHalf = -92
K_Ko  = 1.5
K_Nai = 2.6
Ibar  = 1.4
Pk = extra.Ko / (extra.Ko + K_Ko)
INaK = (1.0 - parameters.INaK_Block) * Ibar * fv * Pna^3 * Pk
    desc: Sodium potassium pump
    in [uA/uF]
    phi = (membrane.V - VHalf) * phys.FRT
    fv  = 1 / (1 + exp(-phi))
    Pna = sodium.Nai / (sodium.Nai + K_Nai)

#
# Sodium Calcium exchanger
#
[inaca]
use extra.Cao
use calciumb.Ca_i
use calciumb.Ca_sr
use common.vf
vMax   = 4.5
kSat   = 0.32
eta    = 0.27
Km_Ca  = 1.25e-4
Km_Nai = 12.3
Km_Nao = 87.5
Km_Cai = 3.6e-3
Km_Cao = 1.3
Na_o3  = extra.Nao ^3
KmNao3 = Km_Nao ^ 3
KmNai3 = Km_Nai ^ 3
Nai3  = sodium.Nai ^ 3
Na_ss3 = sodium.Na_sr ^ 3
e1 = exp(eta * vf)
e2 = exp((eta-1) * vf)
INaCa = (1.0 - parameters.INCX_Block) * (num / (denom1 * denom2 * (denom3 + denom4)))
    num    = 0.8 * vMax * (Nai3 * Cao * e1 - Na_o3 * Ca_i * e2)
    denom1 = 1 + (Km_Ca / Ca_i) ^ 2
    denom2 = 1 + kSat * e2
    denom3 = Km_Cao * Nai3 + KmNao3 * Ca_i + KmNai3 * Cao * (1 + Ca_i / Km_Cai)
    denom4 = Km_Cai * Na_o3 * (1 + Nai3 / KmNai3) + Nai3 * Cao + Na_o3 * Ca_i
    in [uA/uF]
INaCaSS = (1.0 - parameters.INCX_Block) * (num / (denom1 * denom2 * (denom3 + denom4)))
    num    = 0.2 * vMax * (Na_ss3 * Cao * e1 - Na_o3 * Ca_sr * e2)
    denom1 = 1 + (Km_Ca / Ca_sr) ^ 2
    denom2 = 1 + kSat * e2
    denom3 = Km_Cao * Na_ss3 + KmNao3 * Ca_sr + KmNai3 * Cao * (1 + Ca_sr / Km_Cai)
    denom4 = Km_Cai * Na_o3 * (1 + Na_ss3 / KmNai3) + Na_ss3 * Cao + Na_o3 * Ca_sr
    in [uA/uF]

#
# The inward rectifier potassium current
# Described in section J
#
[ik1]
Gbar = 0.5
GK1 = Gbar * sqrt(extra.Ko / 5.4 [mM])
IK1 = (1.0 - parameters.IK1_Block) * GK1 * K1 * dv
    dv = membrane.V - nernst.EK
    ak = 1.02 / (1 + exp(0.2385 * (dv - 59.215)))
    bk = (
            0.49124 * exp(0.08032 * (dv + 5.476))
            + exp(0.06175 * (dv - 594.31))
            ) / (1 + exp(-0.5143 * (dv + 4.753)))
    K1 = ak / (ak + bk)
    in [uA/uF]

#
# IKr
# The rapid component of the delayed rectifier potassium current (IKr)
# Described in section I
#
# This current is modelled with a time-dependent activation and an
# instantaneous inactivation. The formulation is taken from the HRd2004 model.
#
[ikr]
use membrane.V as V
Gbar = 0.0138542 [mS/uF]
G = Gbar * sqrt(extra.Ko / 5.4)
    in [mS/uF]
    desc: Maximum conductivity of IKr
i = 1 / (1 + exp((V + 10) / 15.4))
    desc: Inactivation of IKr
dot(a)  = (inf - a) / tau
    inf = 1 / (1 + exp((V + 10.085) / -4.25))
    tau = 1 / (6e-4 * ta - 3e-4 * tb)
        ta = if(dd == 0, 1 / 0.136, vv / dd)
            vv = V - 1.7384
            dd = 1 - exp(-0.136 * vv)
        tb = if(dd == 0, 1 / 0.1522, vv /dd)
            vv = V + 38.3608
            dd = 1 - exp(0.1522 * vv)
    desc: Activation of IKr
IKr = (1.0 - parameters.IKr_Block) * G * a * i * (V - nernst.EK)
    desc: Rapid delayed rectifier Potassium current
    in [uA/uF]

#
# IKur (IKp) :: The plateau potassium current
# Described in section K
#
[ikur]
use membrane.V as V
Gbar = 2.76e-3
IKur = (1.0 - parameters.IKur_Block) * Gbar * K * (V - nernst.EK)
    K = 1 / (1 + exp((7.488 - V) / 5.98))
    desc: The plateau potassium current
    in [uA/uF]

#
# Ito (Ito1) :: The "4AP-sensitive transient outward K+ current, uA/uF"
# Described in section D
#
[ito]
use membrane.V as V
Gbar = 0.4975
ITo = (1.0 - parameters.Ito_Block) * Gbar * a ^ 3 * i_fast * i_slow * Rto1 * (V - nernst.EK)
    Rto1 = exp(V / 550)
    in [uA/uF]
dot(a) =  (ss - a) / tau
    beta  = 3.5 / (1 + exp( (V + 100) / 29.3814 ))
    alpha = 1 / (1.2089 * (1 + exp( (V - 18.4099) / -29.3814 )))
    tau   = 1 / (alpha + beta)
    ss    = 1 / (1 + exp( (V + 9.437) / -7.133 ))
beta_i = 1 / (9.7953 * (1 + exp( (V + 19) / -9 )))
dot(i_fast) =  alpha * (1 - i_fast) - beta_i * i_fast
    alpha = 0.025 / (1 + exp( (V + 58) / 5 ))
dot(i_slow) =  alpha * (1 - i_slow) - beta_i * i_slow
    alpha = 1 / (250 * (1 + exp( (V + 60) / 5 )))

#
# IClCa (Ito2) : Ca<sup>2+</sup> dependent transient outward Cl- current
# Described in section E
#
[iclca]
use common.vf, common.vff, common.evf
use membrane.V as V
dot(i_fast) =  (ss - i_fast) / 8
    alpha = 0.025 / (1 + exp((V + 58) / 5 ))
    beta  = 1 / (5 * (1 + exp((V + 19) / -9 )))
    ss = alpha / (alpha + beta)
IClCa = Ibar * i_fast * KCaI
    KCaI = 1 - (1 / (1 + irel.Irel^2 / 0.16))
    Ibar = if(evf == 1,
        9e-7 * vff * extra.Clo,
        9e-7 * vff * (chloride.Cl_i - extra.Clo * evf) / (1 - evf))
    in [uA/uF]

#
# Background chloride current
# Described in section N
#
[iclb]
Gbar = 2.25e-4
IClB = Gbar * (membrane.V - nernst.ECl)
    in [uA/uF]

#
# Background Calcium Current
# Described in section M
#
[icab]
use common.vf, common.vff
use calciumb.Ca_i, extra.Cao
ICab = if(e2v == 1,
        1.995e-7 * 2 * phys.F * (Ca_i - 0.341 * extra.Cao),
        1.995e-7 * 4 * vff * (Ca_i * e2v - 0.341 * extra.Cao) / (e2v - 1))
    e2v = exp(2 * vf)
    in [uA/uF]

#
# The Sarcolemmal Calcium Pump
# Described in section L
#
[ipca]
use calciumb.Ca_i
IpCa_bar = 5.75e-2
Km_pCa = 5e-4
IpCa = IpCa_bar * Ca_i / (Km_pCa + Ca_i)
    in [uA/uF]

#
# Cotransporters
# Described in sections H and N
#
[cot]
KClBar  = 1.77e-5 [mM/ms]
NaClBar = 2.46108e-5 [mM/ms]
CTNaCl = NaClBar * z1 / (z1 + z2)
    z1 = (nernst.ENa - nernst.ECl) ^ 4
    z2 = 59494197.640692541
    desc: Na+Cl- cotransporter (section H)
CTKCl = KClBar * z1 / (z1 + z2)
    z1 = nernst.EK - nernst.ECl
    z2 = 87.8251
    desc: K+Cl- cotransporter (section H)

#
# Ca2+ release from JSR to myoplasm
# Described in section P (SR Release)
#
[irel]
dot(Irel) = -(inf + Irel) / tau
    desc: Ca2+ release from JSR to myoplasm
    in [mM/ms]
alpha = beta * 0.1125
beta = 4.75 * (1 + delta)
delta = 1 / (1 + (0.28 / calcium.active)^10)
inf = ical.ICaL * alpha / (1 + (1 / calcium.Ca_jsr)^8)
tau = beta / (1 + (0.0123 / calcium.Ca_jsr))
    

#
# Intracellular subspaces and myoplasmic concentrations
# Described in section Q
#
[diff]
use calciumb.Ca_i, calciumb.Ca_sr, calciumb.Ca_cal
use sodium.Na_sr, sodium.Nai
use chloride.Cl_sr, chloride.Cl_i
tau_diff = 0.2
tau_diff_CaL = 2
Ca = (Ca_sr - Ca_i) / tau_diff
    desc: Diffusion of Ca2+ between SS,SR and the bulk myoplasm
CaL = (Ca_sr - Ca_cal) / tau_diff_CaL
    desc: Diffusion of Ca2+ between SS,CaL and SS,SR
Na = (Na_sr - Nai) / tau_diff
    desc: Diffusion of Na+ between SS,SR and the bulk myoplasm
Cl = (Cl_sr - Cl_i) / tau_diff
    desc: Diffusion of Cl- between SS,SR and the bulk myoplasm

[[protocol]]
# Level  Start    Length   Period   Multiplier
1.0      100      0.5      1000     0

[[script]]
import matplotlib.pyplot as pl
import myokit

#
# This example file plots a single AP using the 2009 model developed by
# Decker et al.
#

# Get model and protocol, create simulation
m = get_model()
p = get_protocol()
s = myokit.Simulation(m, p)

# Run simulation
d = s.run(1000)

# Display the result
pl.figure()
pl.plot(d['engine.time'], d['membrane.V'])
pl.show()

